load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("//tools:cpplint.bzl", "cpplint")
load("//tools:dlinear.bzl", "DLINEAR_NAME", "DLINEAR_VERSION", "all_files", "dlinear_cc_binary", "dlinear_cc_library")

package(default_visibility = ["//visibility:public"])

genrule(
    name = "generate_version_header",
    outs = ["version.h"],
    cmd = "$(location //tools:generate_version_header) %s %s > $@" % (DLINEAR_NAME, DLINEAR_VERSION),
    stamp = True,
    tools = ["//tools:generate_version_header"],
    visibility = ["//visibility:private"],
)

dlinear_cc_library(
    name = "version",
    hdrs = [":generate_version_header"],
)

dlinear_cc_library(
    name = "dlinear_header",
    hdrs = ["dlinear.h"],
    deps = [
        ":version",
        "//dlinear/solver:context",
        "//dlinear/solver:logic",
        "//dlinear/symbolic",
        "//dlinear/util:box",
        "//dlinear/util:config",
    ],
)

dlinear_cc_binary(
    name = "dlinear",
    srcs = ["main.cpp"],
    deps = [
        "//dlinear/solver",
        "//dlinear/solver:solver_output",
        "//dlinear/util:argparser",
        "//dlinear/util:config",
    ],
)

# ----------------------
# Distribution
# ----------------------
cc_shared_library(
    name = "libdlinear.so",
    shared_lib_name = "libdlinear.so",
    deps = [
        "//dlinear/solver",
        "@system_deps//:gmpxx_deps",
        "@system_deps//:mpfr_deps",
    ],
)

all_files(
    name = "all",
    additional_hdrs = ["version.h"],
    deps = [
        "//dlinear/libs:all_headers_tar",
        "//dlinear/parser:all_headers_tar",
        "//dlinear/solver:all_headers_tar",
        "//dlinear/symbolic:all_headers_tar",
        "//dlinear/util:all_headers_tar",
    ],
)

pkg_tar(
    name = "package_headers",
    srcs = [
        ":all_headers",
    ],
    extension = "tar.gz",
    package_dir = "include/dlinear",
    tags = ["manual"],
    deps = [
        "//dlinear/libs:all_headers_tar",
        "//dlinear/parser:all_headers_tar",
        "//dlinear/solver:all_headers_tar",
        "//dlinear/symbolic:all_headers_tar",
        "//dlinear/util:all_headers_tar",
    ],
)

pkg_tar(
    name = "package_bin",
    srcs = [
        ":dlinear",
    ],
    extension = "tar.gz",
    mode = "0755",
    package_dir = "bin",
    tags = ["manual"],
)

pkg_tar(
    name = "package_sharedlib",
    srcs = [
        ":libdlinear.so",
    ],
    extension = "tar.gz",
    package_dir = "lib",
    tags = ["manual"],
)

cpplint()
